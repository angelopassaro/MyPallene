/* Simple +/-/* syntax.expression language; Parser evaluates constant expressions on the fly*/
package core;

import java_cup.runtime.*;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java.util.LinkedList;

import syntax.*;
import syntax.expression.*;
import syntax.expression.binaryexpr.arithop.DivOp;
import syntax.expression.binaryexpr.arithop.MinusOp;
import syntax.expression.binaryexpr.arithop.PlusOp;
import syntax.expression.binaryexpr.arithop.TimesOp;
import syntax.expression.binaryexpr.relop.*;
import syntax.expression.unaryexpr.*;
import syntax.function.*;
import syntax.statement.*;
import syntax.typedenoter.*;

class Parser;

parser code{:

   public void report_error(String message, Object info) {
     if (info instanceof ComplexSymbol) {
       ComplexSymbol cs = (ComplexSymbol)info;
       String msg = String.format("%s for input symbol %s spanning from $s to %s", message, cs.getName(), cs.getLeft(), cs.getRight());
       System.err.println(msg);
     } else {
         System.err.println(message);
     }
   }

   protected void syntax_error() {
     List<Integer> ids = this.expected_token_ids();
     LinkedList<String> list = new LinkedList<>();
     ids.forEach(id -> list.add(this.symbl_name_from_id(id)));
     System.err.println("instead expected token classes are " + list);
    }

:}


/* Terminals (tokens returned by the scanner). */
terminal String FUNCTION, MAIN, END, IF, THEN, ELSE, WHILE, DO, FOR, LOCAL, GLOBAL,READ, WRITE, RETURN, NOT, SHARP, ID; /* Keywords */
terminal String NIL, INT, BOOL, FLOAT, STRING;                                                                          /* Types Keywords */
terminal String LPAR, RPAR, BLPAR, BRPAR, SLPAR, SRPAR, COMMA, SEMI, COLON;                                             /* separators */
terminal String AND, OR, LT, LE, EQ, NE, GT, GE, ARROW, ASSIGN, NOP;                                                    /* relop */
terminal String PLUS, MINUS, TIMES, DIV;                                                                                /* arop */
terminal String UMINUS;

/* Types Keywords */
terminal int                INT_CONST;
terminal float              FLOAT_CONST;
terminal String             STRING_CONST;
terminal boolean            TRUE, FALSE;

/* Non terminals */
non terminal                Program Program;
non terminal                Global Global;
non terminal                LinkedList<Function> Functions;
non terminal                Function Def_fun;
non terminal                LinkedList<ParDecl> Par_decls;
non terminal                LinkedList<VarDecl> Var_decls;
non terminal                VarInitValue Var_init_value;
non terminal                TypeDenoter TypeDenoter;
non terminal                LinkedList<TypeDenoter> Types;
non terminal                LinkedList<Statement> Statements;
non terminal                Statement Stat;
non terminal                LinkedList<Id> Vars;
non terminal                LinkedList<Expr> Exprs;
non terminal                Expr Expr;


/* Precedences */
precedence left PLUS, MINUS, OR;
precedence left TIMES, DIV, AND;
precedence nonassoc LT, LE, EQ, NE, GT, GE;
precedence left UMINUS, NOT, COMMA, SHARP;

/* The grammar rules */

Program             ::= Global:global Functions:functions                                                               {: RESULT = new Program(functionsleft, functionsright, global, functions); :}
                        ;

Global              ::= GLOBAL:g Var_decls:varDecls END                                                                 {: RESULT = new Global(gleft,gright,varDecls); :}
                        | /* empty*/
                        ;

Functions           ::= Def_fun:defFun Functions:functions                                                              {: functions.addFirst(defFun); RESULT = functions; :}
                        | Def_fun:defFun                                                                                {: RESULT = new LinkedList<Function>(); RESULT.addFirst(defFun); :}
                        ;

Def_fun             ::= FUNCTION:f ID:id LPAR Par_decls:parDecl RPAR COLON TypeDenoter:type Statements:statements END   {: RESULT = new ComplexDefFun(fleft, fright, new Id(idleft, idright, id), type, statements, parDecl); :}
                        ;

Def_fun             ::= FUNCTION:f ID:id LPAR RPAR COLON TypeDenoter:type Statements:statements END                     {: RESULT = new SimpleDefFun(fleft, fright,new Id(idleft, idright, id), type, statements);:}
                        ;

Par_decls            ::= ID:id COLON TypeDenoter:type COMMA Par_decls:parDecl                                           {: parDecl.addFirst(new ParDecl(idleft, idright, new Id(idleft, idright, id), type));RESULT = parDecl;:}
                        | ID:id COLON TypeDenoter:type                                                                  {: RESULT = new LinkedList<ParDecl>(); RESULT.addFirst(new ParDecl(idleft, idright, new Id(idleft, idright, id), type));:}
                        ;

Var_decls           ::= ID:id COLON TypeDenoter:type Var_init_value:varInit SEMI Var_decls:varDecls                     {: varDecls.addFirst(new VarDecl(idleft,idright,new Id(idleft, idright, id),type,varInit)); RESULT = varDecls;:}
                        | ID:id COLON TypeDenoter:type Var_init_value:varInit                                           {: RESULT = new LinkedList<VarDecl>(); RESULT.addFirst(new VarDecl(idleft,idright,new Id(idleft, idright, id), type, varInit)); :}
                        ;

Var_init_value      ::= ASSIGN Expr:expr                                                                                {:  RESULT = new VarInitValue(exprleft, exprright, expr); :}
                        | /*empty*/
                        ;

TypeDenoter         ::= NIL:nil                                                                                         {: RESULT = new PrimitiveTypeDenoter(nilleft, nilright, "NIL"); :}
                        | INT:integer                                                                                   {: RESULT = new PrimitiveTypeDenoter(integerleft, integerright, "INT");:}
                        | BOOL:booler                                                                                   {: RESULT = new PrimitiveTypeDenoter(boolerleft, boolerright, "BOOL"); :}
                        | FLOAT:floater                                                                                 {: RESULT = new PrimitiveTypeDenoter(floaterleft, floaterright, "FLOAT");:}
                        | STRING:stringer                                                                               {: RESULT = new PrimitiveTypeDenoter(stringerleft, stringerright, "STRING");:}
                        | BLPAR:b TypeDenoter:type BLPAR                                                                {: RESULT = new ArrayTypeDenoter(bleft, bright, type);:}
                        | LPAR:l Types:types RPAR ARROW TypeDenoter:rtype                                               {: RESULT = new FunctionTypeDenoter(lleft, lright, types, rtype);:} //controllare type
                        | LPAR:l RPAR ARROW TypeDenoter:type                                                            {: RESULT = new FunctionTypeDenoter(typeleft, typeright, type); :}
                        ;

Types               ::= TypeDenoter:type COMMA Types:types                                                              {: types.addFirst(type); RESULT = types;:}
                        | TypeDenoter:type                                                                              {: RESULT = new LinkedList<TypeDenoter>(); RESULT.addFirst(type);:}
                        ;

Statements           ::= Stat:stat SEMI Statements:statements                                                           {: statements.addFirst(stat); RESULT = statements; :}
                        | Stat:stat                                                                                     {: RESULT = new LinkedList<Statement>(); RESULT.addFirst(stat); :}
                        ;

Stat                ::= NOP:nop                                                                                         {: RESULT = new NopStatement(nopleft, nopright); :}
                        | WHILE:w Expr:expr DO Statements:statements END                                                {: RESULT = new WhileStatement(wleft, wright, expr, statements);:}
                        | IF:ifer Expr:expr THEN Statements:statements ELSE Statements:elseStatements END               {: RESULT = new IfThenElseStatement(iferleft, iferright,expr,statements, elseStatements);:}
                        | IF:ifer Expr:expr THEN Statements:statements                                                  {: RESULT = new IfThenStatement(iferleft, iferright, expr, statements); :}
                        | FOR:forer ID:id ASSIGN Expr:expr COMMA Expr:commaExpr DO Statements:statements END            {: RESULT = new ForStatement(forerleft, forerright, new Id(idleft, idright, id), expr, commaExpr, statements);:}
                        | LOCAL:local Var_decls:varDecls SEMI Statements:statements END                                 {: RESULT = new LocalStatement(localleft, localright, varDecls, statements);:}
                        | ID:id ASSIGN Expr:expr                                                                        {: RESULT = new AssignStatement(idleft, idright, new Id(idleft, idright, id), expr);:}
                        | Expr:expr SLPAR Expr:arrayExpr SRPAR ASSIGN Expr:assignExpr                                   {: RESULT = new ArrayElementStatement(exprleft, exprright, arrayExpr, expr, assignExpr);:}
                        | ID:id LPAR Exprs:exprs RPAR                                                                   {: RESULT = new FunctionCallStatement(idleft, idright, new Id(idleft, idright, id) ,exprs);:}
                        | Vars:vars READ                                                                                {: RESULT = new ReadStatement(varsleft, varsright, vars); :}
                        | Exprs:exprs WRITE                                                                             {: RESULT = new WriteStatement(exprsleft, exprsright, exprs); :}
                        | RETURN Expr:expr                                                                              {: RESULT = new ReturnStatement(exprleft, exprright, expr); :}
                        ;

Vars                ::= ID:id COMMA Vars:vars                                                                           {: vars.addFirst(new Id(idleft, idright, id));RESULT = vars; :}
                        | ID:id                                                                                         {: RESULT = new LinkedList<Id>(); RESULT.addFirst(new Id(idleft, idright, id));:}
                        ;
Exprs               ::= Expr:expr COMMA Exprs:exprs                                                                     {: exprs.addFirst(expr); RESULT = exprs;:}
                        | Expr:expr                                                                                     {: RESULT = new LinkedList<Expr>(); RESULT.addFirst(expr);:}
                        ;

Expr                ::= NIL:nil                                                                                         {: RESULT = new NilConst(nilleft, nilright); :}
                        | TRUE:truer                                                                                    {: RESULT = new BooleanConst(truerleft, truerright, true);:}
                        | FALSE:falser                                                                                  {: RESULT = new BooleanConst(falserleft, falserright, false);:}
                        | INT_CONST:intConst                                                                            {: RESULT = new IntegerConst(intConstleft, intConstright, intConst);:}
                        | FLOAT_CONST:floatConst                                                                        {: RESULT = new FloatConst(floatConstleft, floatConstright, floatConst);:}
                        | STRING_CONST:stringConst                                                                      {: RESULT = new StringConst(stringConstleft, stringConstright, stringConst);:}
                        | BLPAR:b BRPAR COLON Type:type                                                                 {: RESULT = new ArrayConst(bleft, bright, type);:}
                        | ID:id                                                                                         {: RESULT = new Id(idleft, idright, id);:}
                        | Expr:expr SLPAR Expr:exprElem SRPAR                                                           {: RESULT = new ArrayRead(exprleft, exprright, expr, exprElem);:}
                        | ID:id LPAR Exprs:exprs RPAR                                                                   {: RESULT = new FunctionCall(idleft, idright, new Id(idleft, idright, id), exprs);:}
                        | Expr:e1 PLUS Expr:e2                                                                          {: RESULT = new PlusOp(e1left, e1right, e1, e2);:}
                        | Expr:e1 MINUS Expr:e2                                                                         {: RESULT = new MinusOp(e1left, e1right, e1, e2);:}
                        | Expr:e1 TIMES Expr:e2                                                                         {: RESULT = new TimesOp(e1left, e1right, e1, e2);:}
                        | Expr:e1 DIV Expr:e2                                                                           {: RESULT = new DivOp(e1left, e1right, e1, e2); :}
                        | Expr:e1 AND Expr:e2                                                                           {: RESULT = new AndOp(e1left, e1right, e1, e2);:}
                        | Expr:e1 OR Expr:e2                                                                            {: RESULT = new OrOp(e1left, e1right, e1, e2);:}
                        | Expr:e1 GT Expr:e2                                                                            {: RESULT = new GtOp(e1left, e1right, e1, e2); :}
                        | Expr:e1 GE Expr:e2                                                                            {: RESULT = new GeOp(e1left, e1right, e1, e2);:}
                        | Expr:e1 LT Expr:e2                                                                            {: RESULT = new LtOp(e1left, e1right, e1, e2);:}
                        | Expr:e1 LE Expr:e2                                                                            {: RESULT = new LeOp(e1left, e1right, e1, e2);:}
                        | Expr:e1 EQ Expr:e2                                                                            {: RESULT = new EqOp(e1left, e1right, e1, e2);:}
                        | Expr:e1 NE Expr:e2                                                                            {: RESULT = new NeOp(e1left, e1right, e1, e2);:}
                        | MINUS Expr:expr                                                                               {: RESULT = new UMinusExpression(exprleft, exprright,expr);:}
                        %prec UMINUS
                        | NOT Expr:expr                                                                                 {: RESULT = new NotExpression(exprleft, exprright, expr);:}
                        | SHARP Expr:expr                                                                               {: RESULT = new SharpExpression(exprleft, exprright, expr);:}
                        ;